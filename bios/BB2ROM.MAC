;
; BigBoard II Monitor
;
	ASEG
;
MONITR	EQU	0F000H
DSKBUF	EQU	0F800H
RAM	EQU	0FF00H
;
	ORG	00000H
	.Z80
;
; COLD START INITIALIZATION
;
INIT:	DI
	LD	SP,TSTSTK
	LD	DE,0
	LD	A,010H
	JP	RAMTST
;
INIT1:	JR	NZ,INIT1
;
RAMTST:	LD	HL,MONITR
	LD	DE,RAM
	LD	A,1
RTST1:	LD	B,8
RTST2:	LD	(HL),A
	LD	(DE),A
	RLCA
	INC	HL
	INC	E
	JR	NZ,RTST2
	DJNZ	RTST2
	LD	C,8
RTST3:	DEC	HL
	DEC	E
	RRCA
	CP	(HL)
	JR	NZ,$
	EX	DE,HL
	CP	(HL)
	EX	DE,HL
	JR	NZ,$
	DJNZ	RTST3
	DEC	C
	JR	NZ,RTST3
	ADD	A,A
	JR	NZ,RTST1
;
	LD	SP,STACK
	LD	HL,MONCOPY
	LD	DE,MONITR
	LD	BC,MONLEN
	LDIR
	LD	HL,RAMCOPY
	LD	DE,RAM
	LD	BC,RAMLEN
	LDIR
;
	LD	HL,IOTAB
INIT3:	LD	B,(HL)
	INC	HL
	LD	C,(HL)
	INC	HL
	OTIR
	BIT	7,(HL)
	JR	Z,INIT3
;
	LD	HL,M1TAB
	IN	A,(SENSE)
	BIT	4,A
	JR	Z,SKIP1
	LD	HL,M2TAB
SKIP1:	CALL	INITCRT
	CALL	CLRALL
;
	IN	A,(SENSE)
	BIT	5,A
	JR	Z,SKIP2
	LD	A,00000011B
	LD	(DSKTYP),A
	LD	A,15
	LD	(NREVS),A
	LD	A,5*20
	LD	(NSTOP),A
SKIP2:	CALL	FLIPDENS
;
	LD	HL,RAM
	LD	A,H
	LD	I,A
	IM	2
;
	LD	DE,01000H
	LD	A,018H
	JP	NOROM2
;
	CALL	Z,01000H
;
NOROM2:	CALL	BELL
	IN	A,(SENSE)
	AND	00001011B
	LD	C,A
DECIDE:	IN	A,(SENSE)
	AND	00001011B
	XOR	C
	JR	Z,DECIDE
	BIT	3,A
	JR	NZ,PARALL
;
BAUD:	AND	00000011B
	LD	C,A
	LD	B,0
BAUD1:	IN	A,(SENSE)
	AND	C
	JR	Z,BAUD1
BAUD2:	EX	(SP),HL
	EX	(SP),HL
	INC	B
	IN	A,(SENSE)
	AND	C
	JR	NZ,BAUD2
	LD	DE,RATES-1
BAUD3:	INC	DE
	RL	B
	JR	NC,BAUD3
	LD	HL,SIOATAB
	BIT	0,C
	JR	NZ,BAUD4
	LD	HL,SIOBTAB
BAUD4:	LD	C,(HL)
	INC	HL
	LD	A,01000111B
	OUT	(C),A
	LD	A,(DE)
	OUT	(C),A
	LD	E,(HL)
	INC	HL
	LD	D,(HL)
	INC	HL
	LD	(SIOADR),DE
	LD	C,E
BAUD5:	IN	A,(C)
	BIT	0,A
	JR	Z,BAUD5
	LD	C,D
	IN	A,(C)
	LD	C,E
	LD	A,1
	OUT	(C),A
	LD	A,00011100B
	OUT	(C),A
	LD	A,(HL)
	LD	(CONNUM),A
	LD	HL,SIOOUT
	LD	(CONOUT+1),HL
	JR	SIGNON
;
RATES:	DEFB	128
	DEFB	64
	DEFB	32
	DEFB	16
	DEFB	8
	DEFB	4
	DEFB	2
	DEFB	2
;
SIOATAB:
	DEFB	CTCB1
	DEFB	SIOCPA
	DEFB	SIODPA
	DEFB	1
;
SIOBTAB:
	DEFB	CTCB0
	DEFB	SIOCPB
	DEFB	SIODPB
	DEFB	2
;
PARALL:	IN	A,(SENSE)
	XOR	C
	BIT	3,A
	JR	NZ,PARALL
PARL3:	LD	A,11000111B
	BIT	3,C
	JR	NZ,PARL4
	SET	4,A
PARL4:	OUT	(CTCA0),A
	LD	A,001H
	OUT	(CTCA0),A
SIGNON:	EI
	CALL	PNEXT
	DEFB	CR,LF,LF
	DEFB	'... BB_II V1.1  System Monitor  9-March-83 ..'
	DEFB	CR,LF,LF
	DEFB	EOT
	JP	PROMPT
;
	DEFW	INIT1
TSTSTK	EQU	$
;
CRCGEN:	LD	HL,-1
CRCG1:	LD	B,8
	EX	AF,AF'
	LD	A,(DE)
	LD	C,A
CRCG2:	LD	A,C
	XOR	H
	RLCA
	ADC	HL,HL
	RRCA
	JR	NC,CRCG3
	LD	A,H
	XOR	00010000B
	LD	H,A
	LD	A,L
	XOR	00100000B
	LD	L,A
CRCG3:	RL	C
	DJNZ	CRCG2
	EX	AF,AF'
	INC	DE
	CP	D
	JR	NZ,CRCG1
	LD	A,H
	OR	L
	RET
;
INITCRT:
	LD	B,16
	LD	C,0
INCRT1:	LD	A,C
	OUT	(CRTADD),A
	LD	A,(HL)
	INC	HL
	OUT	(CRTDAT),A
	INC	C
	DJNZ	INCRT1
	RET
;
SIO	EQU	80H
CTCA	EQU	84H
CTCB	EQU	88H
DMA	EQU	8CH
PORT3	EQU	0C0H
SENSE	EQU	0C4H
PORT0	EQU	0C8H
SELMUX	EQU	0CCH
KBD	EQU	0D0H
WD179X	EQU	0D4H
GENPIO	EQU	0D8H
CRTC	EQU	0DCH
;
SIODPA	EQU	SIO+0
SIOCPA	EQU	SIO+1
SIODPB	EQU	SIO+2
SIOCPB	EQU	SIO+3
;
CTCA0	EQU	CTCA
CTCA1	EQU	CTCA+1
CTCA2	EQU	CTCA+2
CTCA3	EQU	CTCA+3
;
CTCB0	EQU	CTCB
CTCB1	EQU	CTCB+1
CTCB2	EQU	CTCB+2
CTCB3	EQU	CTCB+3
;
ON	EQU	00001000B
OFF	EQU	00000000B
;
BUZZER	EQU	7
MOTOR	EQU	6
HLDTIM	EQU	5
DDEN	EQU	4
SMC2	EQU	3
SMC1	EQU	2
SIDSEL	EQU	1
DYSTAT	EQU	0
;
ATTEN	EQU	7
FDCRST	EQU	6
STDBB	EQU	5
VPP	EQU	4
PGM	EQU	3
OEVPP	EQU	2
DECODE	EQU	1
OUTEN	EQU	0
;
IOTAB	EQU	$
;
	DEFB	2,PORT3
	DEFB	ATTEN+ON
	DEFB	FDCRST+ON

	DEFB	1,SELMUX
	DEFB	00001010B

	DEFB	6,DMA
	DEFB	11000011B
	DEFB	11000011B
	DEFB	11000011B
	DEFB	11000011B
	DEFB	11000011B
	DEFB	11000011B

	DEFB	1,CTCA0
	DEFB	CTCAV

	DEFB	2,CTCA1
	DEFB	01000111B
	DEFB	0

	DEFB	1,CTCB0
	DEFB	CTCBV

	DEFB	2,CTCB0
	DEFB	01000111B
	DEFB	128

	DEFB	2,CTCB1
	DEFB	01000111B
	DEFB	128

	DEFB	2,CTCB2
	DEFB	00000111B
	DEFB	250

	DEFB	2,CTCB3
	DEFB	11000111B
	DEFB	250

	DEFB	10,SIOCPB
	DEFB	4
	DEFB	01000101B
	DEFB	1
	DEFB	00000100B
	DEFB	3
	DEFB	01000001B
	DEFB	5
	DEFB	10101010B
	DEFB	2
	DEFB	SIOVEC

	DEFB	8,SIOCPA
	DEFB	4
	DEFB	01000101B
	DEFB	1
	DEFB	00000000B
	DEFB	3
	DEFB	01000001B
	DEFB	5
	DEFB	10101010B

	DEFB	-1

CHRMEM	EQU	6000H
ATTMEM	EQU	7000H

CRTADD	EQU	CRTC
CRTDAT	EQU	CRTC+1

BLINK	EQU	01000000B
REVERSE	EQU	00010000B
UNDLINE	EQU	00001000B
STRIKE	EQU	00000100B

M1TAB:	DEFB	102
	DEFB	80
	DEFB	89
	DEFB	28H
	DEFB	25
	DEFB	10
	DEFB	24
	DEFB	24
	DEFB	00000000B
	DEFB	9
	DEFB	00100000B
	DEFB	0
	DEFB	0
	DEFB	0
	DEFB	0
	DEFB	0

M2TAB:	DEFB	112
	DEFB	80
	DEFB	96
	DEFB	28H
	DEFB	25
	DEFB	3
	DEFB	24
	DEFB	24
	DEFB	00000000B
	DEFB	11
	DEFB	00100000B
	DEFB	0
	DEFB	0
	DEFB	0
	DEFB	0
	DEFB	0

CRTOUT:	LD	HL,(ESCVEC)
	LD	A,H
	OR	L
	JR	NZ,CRT2
	RES	7,C
	LD	A,C
	CP	' '
	JR	C,CRT3
	CALL	DISPLAY
	RET
;
CRT2:	LD	DE,0
	LD	(ESCVEC),DE
	JP	(HL)
;
CRT3:	LD	HL,CTLTAB
	LD	BC,CTLSIZ/3
	CALL	SEARCH
	RET	NZ
	JP	(HL)
;
CTLTAB:	DEFB	'G'-64
	DEFB	'H'-64
	DEFB	'I'-64
	DEFB	'J'-64
	DEFB	'K'-64
	DEFB	'L'-64
	DEFB	'M'-64
	DEFB	'Q'-64
	DEFB	'X'-64
	DEFB	'Z'-64
	DEFB	'['-64
	DEFB	'^'-64
	DEFB	'_'-64

	DEFW	STUFF
	DEFW	HOMEUP
	DEFW	ESCAPE
	DEFW	CLRALL
	DEFW	CLREOL
	DEFW	CLREOS
	DEFW	RETURN
	DEFW	RIGHT
	DEFW	UPLINE
	DEFW	DNLINE
	DEFW	HTAB
	DEFW	LEFT
	DEFW	BELL
CTLSIZ	EQU	$-CTLTAB
;
DISPLAY:
	LD	HL,(CURSOR)
	LD	DE,CHRMEM
	ADD	HL,DE
	EX	DE,HL
	LD	HL,4096
	ADD	HL,DE
	EX	DE,HL
	LD	A,(ATTRIB)
	LD	(HL),C
	LD	(DE),A
	LD	A,(COL)
	INC	A
	CP	80
	JR	NC,DISP2
	LD	(COL),A
	LD	HL,(CURSOR)
	INC	HL
	RES	3,H
	LD	(CURSOR),HL
	CALL	SETCSR
	RET
;
DISP2:	LD	HL,CRTFLG
	BIT	AUTONL,(HL)
	RET	Z
	XOR	A
	LD	(COL),A
	CALL	DNLINE
	RET
;
BELL:	LD	A,BUZZER+ON
	OUT	(PORT0),A
	LD	BC,200
BELL2:	DJNZ	BELL2
	DEC	C
	JR	NZ,BELL2
	LD	A,BUZZER+OFF
	OUT	(PORT0),A
	RET
;
ESCAPE:	LD	HL,ESCSEQ
	LD	(ESCVEC),HL
	RET
;
STUFF:	LD	HL,DISPLAY
	LD	(ESCVEC),HL
	RET
;
HTAB:	LD	A,(COL)
	AND	00000111B
	LD	C,A
	LD	A,8
	SUB	C
	LD	D,A
	LD	E,0
	JR	CSRMOV
;
LEFT:	LD	DE,0FF00H
	JR	CSRMOV
;
RIGHT:	LD	DE,0100H
	JR	CSRMOV
;
UP:	LD	DE,00FFH
	JR	CSRMOV
;
DOWN:	LD	DE,0001H
CSRMOV:	LD	HL,(RC)
	LD	A,L
	ADD	A,E
	CP	24
	RET	NC
	LD	L,A
	LD	A,H
	ADD	A,D
	CP	80
	RET	NC
	LD	H,A
GOTOXY:	LD	(RC),HL
	LD	B,0
	LD	C,H
	LD	H,0
	CALL	MULT80
	ADD	HL,BC
	LD	DE,(START)
	ADD	HL,DE
	LD	A,H
	AND	00000111B
	LD	H,A
	LD	(CURSOR),HL
SETCSR:	LD	A,(MOVECS)
	OR	A
	RET	NZ
	LD	A,11010111B
	OUT	(CTCA3),A
	LD	A,1
	OUT	(CTCA3),A
	LD	(MOVECS),A
	RET
;
ZAPCSR:	DI
	LD	A,00000011B
	OUT	(CTCA3),A
	EI
	XOR	A
	LD	(MOVECS),A
	LD	HL,(CURSOR)
	LD	DE,ATTMEM
T0300:	ADD	HL,DE
	LD	A,(HL)
	BIT	7,A
T0304:	RET	Z
	XOR	10010000B
	LD	(HL),A
	RET
;
MULT80:	ADD	HL,HL
	ADD	HL,HL
	ADD	HL,HL
	ADD	HL,HL
	LD	D,H
	LD	E,L
	ADD	HL,HL
	ADD	HL,HL
	ADD	HL,DE
	RET
;
UPLINE:	LD	A,(ROW)
	OR	A
	JR	NZ,UP
	LD	DE,-80
	LD	BC,0
	JR	LFEED
;
DNLINE:	LD	A,(ROW)
	CP	23
	JR	NZ,DOWN
	LD	DE,80
	LD	BC,23*80
LFEED:	LD	HL,CRTFLG
	BIT	NOSCRL,(HL)
	RET	NZ
	LD	(NEWLIN),BC
	LD	HL,(START)
	ADD	HL,DE
	LD	A,H
	AND	00000111B
	LD	H,A
	LD	(START),HL
	LD	A,1
	LD	(SCROLL),A
	LD	HL,(RC)
	CALL	GOTOXY
LFEED2:	LD	A,(SCROLL)
	OR	A
	JR	NZ,LFEED2
	RET
;
RETURN:	LD	HL,(RC)
	LD	H,0
	CALL	GOTOXY
	RET
;
CLRLINE:
	LD	DE,CHRMEM
	ADD	HL,DE
	EX	DE,HL
	LD	HL,4096
	ADD	HL,DE
	EX	DE,HL
	LD	A,(BLANK)
	LD	(HL),A
	LD	A,(ATTRIB)
	LD	(DE),A
	DEC	BC
	LD	A,B
	OR	C
	RET	Z
	PUSH	DE
	PUSH	BC
	LD	D,H
	LD	E,L
	INC	DE
	LDIR
	POP	BC
	POP	HL
	LD	D,H
	LD	E,L
	INC	DE
	LDIR
	RET
;
CLREOS:	LD	A,23
	LD	HL,ROW
	SUB	(HL)
	LD	H,0
	LD	L,A
	CALL	MULT80
	EX	DE,HL
	JR	CLR2
;
CLREOL:	LD	DE,0
CLR2:	LD	A,80
	LD	HL,COL
	SUB	(HL)
	LD	H,0
	LD	L,A
	ADD	HL,DE
	LD	B,H
	LD	C,L
	LD	HL,(CURSOR)
	CALL	CLRLINE
	CALL	SETCSR
	RET
;
CLRALL:	LD	HL,CHRMEM
	LD	DE,CHRMEM+1
	LD	BC,2047
	LD	A,(BLANK)
	LD	(HL),A
	LDIR
	LD	HL,ATTMEM
	LD	DE,ATTMEM+1
	LD	BC,2047
	LD	A,(ATTRIB)
	LD	(HL),A
	LDIR
HOMEUP:	LD	HL,0
	CALL	GOTOXY
	RET
;
COMPRESS:
	PUSH	BC
	PUSH	DE
	PUSH	HL
	PUSH	BC
	LD	BC,CHRMEM
	ADD	HL,BC
	EX	DE,HL
	ADD	HL,BC
	EX	DE,HL
	POP	BC
	LDIR
	CALL	ZAPCSR
	POP	HL
	POP	DE
	LD	BC,ATTMEM
	ADD	HL,BC
	EX	DE,HL
	ADD	HL,BC
	EX	DE,HL
	POP	BC
	LDIR
	RET
;
EXPAND:	PUSH	BC
	PUSH	DE
	PUSH	HL
	PUSH	BC
	LD	BC,CHRMEM
	ADD	HL,BC
	EX	DE,HL
	ADD	HL,BC
	EX	DE,HL
	POP	BC
	LDDR
	CALL	ZAPCSR
	POP	HL
	POP	DE
	LD	BC,ATTMEM
	ADD	HL,BC
	EX	DE,HL
	ADD	HL,BC
	EX	DE,HL
	POP	BC
	LDDR
	RET
;
DELLINE:
	CALL	RETURN
	LD	A,23
	LD	HL,ROW
	SUB	(HL)
	JP	Z,CLREOL
	LD	H,0
	LD	L,A
	CALL	MULT80
	LD	B,H
	LD	C,L
D0420:	LD	DE,(CURSOR)
	LD	HL,80
	ADD	HL,DE
	CALL	COMPRESS
	LD	HL,(START)
	LD	DE,23*80
	ADD	HL,DE
	LD	A,H
	AND	00000111B
	LD	H,A
	LD	BC,80
	CALL	CLRLINE
	CALL	SETCSR
	RET
;
INSLINE:
	CALL	RETURN
	LD	A,23
	LD	HL,ROW
	SUB	(HL)
	JP	Z,CLREOL
	LD	H,0
	LD	L,A
	CALL	MULT80
	LD	B,H
	LD	C,L
	LD	HL,(CURSOR)
	ADD	HL,BC
	DEC	HL
	EX	DE,HL
	LD	HL,80
	ADD	HL,DE
	EX	DE,HL
	CALL	EXPAND
	CALL	CLREOL
	RET
;
DELCHAR:
	LD	HL,COL
	LD	A,79
	SUB	(HL)
	JP	Z,CLREOL
	LD	B,0
	LD	C,A
	PUSH	BC
	LD	HL,(CURSOR)
	LD	D,H
	LD	E,L
	INC	HL
	CALL	COMPRESS
	POP	BC
	LD	HL,(CURSOR)
	ADD	HL,BC
	LD	BC,1
	CALL	CLRLINE
	CALL	SETCSR
	RET
;
INSCHAR:
	LD	HL,COL
	LD	A,79
	SUB	(HL)
	JP	Z,CLREOL
	LD	B,0
	LD	C,A
	LD	HL,(CURSOR)
	ADD	HL,BC
	LD	D,H
	LD	E,L
	DEC	HL
	CALL	EXPAND
	LD	HL,(CURSOR)
	LD	BC,1
	CALL	CLRLINE
	CALL	SETCSR
	RET
;
ESCSEQ:	LD	A,C
	LD	HL,ESCTAB
	LD	BC,ESCSIZ/3
	CALL	SEARCH
	RET	NZ
	JP	(HL)
;
ESCTAB:	DEFB	'='
	DEFB	'Q'
	DEFB	'W'
	DEFB	'E'
	DEFB	'R'
	DEFB	'*'
	DEFB	':'
	DEFB	'T'
	DEFB	't'
	DEFB	'Y'
	DEFB	'y'
	DEFB	'G'
	DEFB	'M'
	DEFB	'.'

	DEFW	SETPARM
	DEFW	SETMODE
	DEFW	SETATTR
	DEFW	CLREOS
	DEFW	CLREOS
	DEFW	CLREOL
	DEFW	CLREOL
	DEFW	CLRALL
	DEFW	CLRALL
	DEFW	DELLINE
	DEFW	INSLINE
	DEFW	DELCHAR
	DEFW	INSCHAR
	DEFW	SETRC
ESCSIZ	EQU	$-ESCTAB

SETRC:	LD	HL,SETROW
	LD	(ESCVEC),HL
	RET
;
SETROW:	LD	A,C
	SUB	' '
	RET	C
SETR2:	SUB	24
	JR	NC,SETR2
	ADD	A,24
	LD	(ROWTMP),A
	LD	HL,SETCOL
	LD	(ESCVEC),HL
	RET
;
SETCOL:	LD	A,C
	SUB	' '
	RET	C
SETC2:	SUB	80
	JR	NC,SETC2
	ADD	A,80
	LD	H,A
	LD	A,(ROWTMP)
	LD	L,A
	CALL	GOTOXY
	RET
;
SETATTR:
	LD	HL,SATTR2
	LD	(ESCVEC),HL
	RET
;
SATTR2:	LD	A,C
	CALL	ASCHEX
	RET	C
	LD	B,0
	LD	C,A
	LD	HL,BLANK
	LD	(HL),' '
	LD	A,00000011B
	BIT	3,C
	JR	Z,SATTR3
	LD	(HL),0
	LD	A,(GRMODE)
SATTR3:	LD	HL,BITTAB
	RES	3,C
	ADD	HL,BC
	OR	(HL)
	LD	(ATTRIB),A
	RET
;
BITTAB:	DEFB	0
	DEFB	UNDLINE
	DEFB	BLINK
	DEFB	BLINK+UNDLINE
	DEFB	REVERSE
	DEFB	REVERSE+UNDLINE
	DEFB	REVERSE+BLINK
	DEFB	REVERSE+BLINK+UNDLINE
;
SETMODE:
	LD	HL,SMODE2
	LD	(ESCVEC),HL
	RET
;
SMODE2:	LD	A,C
	AND	00000011B
	LD	(GRMODE),A
	RET
;
SETPARM:
	LD	HL,SPARM2
	LD	(ESCVEC),HL
	RET
;
SPARM2:	LD	A,C
	CALL	ASCHEX
	RET	C
	LD	C,A
	AND	00000111B
	INC	A
	LD	B,A
	XOR	A
	SCF
SPARM3:	RLA
	DJNZ	SPARM3
	LD	HL,CRTFLG
	BIT	3,C
	JR	Z,SPARM4
	OR	(HL)
	LD	(HL),A
	RET
;
SPARM4:	CPL
	AND	(HL)
	LD	(HL),A
	RET
;
PROMPT:	CALL	PNEXT
	DEFB	CR,LF,'* ',EOT
	LD	HL,LINBUF
	LD	C,LINSIZ
	CALL	GETLIN
	JR	C,WHAT
	CALL	CRLFS
	XOR	A
	LD	(BREAK),A
	LD	A,(LINBUF)
	LD	IY,LINBUF+1
	LD	HL,CMDTAB
	LD	BC,CMDSIZ/3
	CALL	SEARCH
	CALL	NZ,EXTEND
	JR	NZ,WHAT
	PUSH	HL
	CALL	PARAMS
	POP	IX
	JR	C,WHAT
	LD	HL,(PARAM1)
	LD	DE,(PARAM2)
	LD	BC,(PARAM3)
	CALL	CALLX
	JR	NC,PROMPT
WHAT:	CALL	PNEXT
	DEFM	' what ?'
	DEFB	EOT
	JR	PROMPT
;
EXTEND:	LD	HL,(MONVEC)
	CALL	DISPATCH
EXT2:	RET
;
CALLX:	JP	(IX)
;
CMDTAB:	DEFB	'V'
	DEFB	'R'
	DEFB	'O'
	DEFB	'I'
	DEFB	'G'
	DEFB	'T'
	DEFB	'F'
	DEFB	'M'
	DEFB	'C'
	DEFB	'B'
	DEFB	'D'
	DEFB	'X'
	DEFB	CR

	DEFW	DUMMY
	DEFW	BANKSW
	DEFW	MEMDMP
	DEFW	BOOT
	DEFW	BLOCK
	DEFW	VIEW
	DEFW	FILL
	DEFW	TEST
	DEFW	GOTO
	DEFW	INCMD
	DEFW	OUTCMD
	DEFW	DSKCMD
	DEFW	VERCMD
CMDSIZ	EQU	$-CMDTAB

DSKCMD:	CP	3
	JR	Z,DSK1
	OR	A
	SCF
	RET	NZ
	LD	HL,SECTOR
	INC	(HL)
	JR	DSK2B
;
DSK1:	LD	C,L
	CALL	SELECT
	JR	NZ,DSKERR
	LD	HL,PARAM2
	LD	C,(HL)
	CALL	SEEK
	JR	NZ,DSKERR
	LD	HL,PARAM3
DSK2B:	LD	C,(HL)
	LD	HL,DSKBUF
	CALL	READ
	JR	NZ,DSKERR
	INC	HL
	EX	DE,HL
	LD	B,4
DSK2C:	SRL	D
	RR	E
	DJNZ	DSK2C
	LD	HL,DSKBUF
	CALL	DUMP
	JR	DSKADR
;
DSKERR:	PUSH	AF
	CALL	PNEXT
	DEFM	'disk error  '
	DEFB	EOT
	POP	AF
	CALL	PUT2HS
DSKADR:	LD	A,(DSKTYP)
	BIT	0,A
	LD	A,'S'
	JR	Z,DSKAD1
	LD	A,'D'
DSKAD1:	CALL	OUTPUT
	LD	HL,DADMSG
	CALL	PMSG
	LD	A,(UNIT)
	CALL	PUT2HS
	LD	A,'T'
	CALL	OUTPUT
	LD	A,(TRACK)
	CALL	PUT2HS
	LD	A,'S'
	CALL	OUTPUT
	LD	A,(SECTOR)
	CALL	PUT2HS
DUMMY:	OR	A
	RET
;
DADMSG:	DEFB	'D  U',EOT
;
BOOT:	LD	C,0
	CALL	SELECT
	JR	NZ,DSKERR
	CALL	HOME
	JR	NZ,DSKERR
	LD	HL,DSKBUF
	LD	C,1
	CALL	READ
	JR	NZ,DSKERR
	LD	HL,CONFIG
	SET	0,(HL)
	LD	HL,DSKBUF
	LD	DE,0080H
	LD	BC,128
	CALL	MOVE
	DI
	LD	SP,STACK
	LD	HL,ROLLIN
	PUSH	HL
	LD	HL,0080H
	PUSH	HL
	JP	EXITMON
;
MEMDMP:	DEC	A
	JR	Z,MDMP2
	DEC	A
	JR	Z,MDMP3
	LD	HL,(LAST)
MDMP2:	LD	DE,16
	JR	MDMP3B
;
MDMP3:	EX	DE,HL
	SBC	HL,DE
	LD	B,4
MDMP3A:	SRL	H
	RR	L
	DJNZ	MDMP3A
	INC	HL
	EX	DE,HL
MDMP3B:	CALL	DUMP
	LD	(LAST),HL
	RET
;
DUMP:	PUSH	HL
	CALL	PUT4HS
	CALL	SPACE
	LD	B,16
DUMP2:	CALL	LOAD
	LD	A,C
	INC	HL
	CALL	PUT2HS
	DJNZ	DUMP2
	POP	HL
	LD	B,16
DUMP3:	CALL	LOAD
	LD	A,C
	INC	HL
	RES	7,A
	CP	020H
	JR	C,DUMP4
	CP	07FH
	JR	C,DUMP5
DUMP4:	LD	A,'.'
DUMP5:	CALL	OUTPUT
	DJNZ	DUMP3
	CALL	CRLFS
	RET	NZ
	DEC	DE
	LD	A,D
	OR	E
	JR	NZ,DUMP
	RET
;
INCMD:	DEC	A
	SCF
	RET	NZ
	LD	C,L
IN1:	CALL	CRLFS
	LD	A,C
	CALL	PUT2HS
	IN	A,(C)
	CALL	PUT2HS
	CALL	ECHO
	CP	CR
	JR	Z,IN2
	CP	'-'
	JR	Z,IN3
	OR	A
	RET
;
IN2:	INC	C
	INC	C
IN3:	DEC	C
	JR	IN1
;
OUTCMD:	CP	2
	SCF
	RET	NZ
	LD	C,L
	OUT	(C),E
	OR	A
	RET
;
VIEW:	CALL	MDATA
	CALL	ECHO
	CP	CR
	JR	Z,VIEW4
	CP	'-'
	JR	Z,VIEW5
	CP	','
	JR	NZ,VIEW2
	CALL	ECHO
	JR	VIEW3
;
VIEW2:	CALL	ASCHEX
	CCF
	RET	NC
	RLCA
	RLCA
	RLCA
	RLCA
	LD	C,A
	CALL	ECHO
	CALL	ASCHEX
	CCF
	RET	NC
	OR	C
VIEW3:	LD	C,A
	CALL	STORE
VIEW4:	INC	HL
	INC	HL
VIEW5:	DEC	HL
	JR	VIEW
;
GOTO:	OR	A
	SCF
	RET	Z
	DI
	LD	SP,STACK
	LD	DE,ROLLIN
	PUSH	DE
	PUSH	HL
	LD	A,(PARAM2)
	LD	HL,(PARAM3)
	LD	DE,(PARAM4)
	LD	BC,(PARAM5)
	JP	EXITMON
;
TEST:	CP	2
	SCF
	RET	NZ
	INC	DE
	LD	E,D
	LD	D,H
	LD	B,0
TEST1:	LD	H,D
	LD	L,0
TEST2:	CALL	MEMTEST
	JR	Z,TEST4
TEST3:	CALL	LOAD
	LD	A,L
	XOR	H
	XOR	B
	CALL	CHECK
	RET	NZ
	INC	HL
	LD	A,H
	CP	E
	JR	NZ,TEST3
TEST4:	INC	B
	LD	A,'+'
	CALL	OUTPUT
	JR	Z,TEST1
	RET
;
CHECK:	CP	C
	RET	Z
	PUSH	AF
	CALL	MDATA
	CALL	PNEXT
	DEFM	'should='
	DEFB	EOT
	POP	AF
	CALL	PUT2HS
	RET
;
MDATA:	CALL	CRLFS
	CALL	PUT4HS
	CALL	LOAD
	LD	A,C
	CALL	PUT2HS
	RET
;
FILL:	CP	3
	SCF
	RET	NZ
	EX	DE,HL
	OR	A
	SBC	HL,DE
	EX	DE,HL
	RET	C
	RET	Z
	CALL	STORE
	LD	B,D
	LD	C,E
	LD	D,H
	LD	E,L
	INC	DE
	CALL	MOVE
	OR	A
	RET
;
BLOCK:	CP	3
	SCF
	RET	NZ
	CALL	BLOCAD
	LD	A,C
	OR	B
	RET	Z
	CALL	MOVE
	OR	A
	RET
;
BLOCAD:	EX	DE,HL
	OR	A
	SBC	HL,DE
	EX	DE,HL
	PUSH	DE
	PUSH	BC
	POP	DE
	POP	BC
	INC	BC
	RET
;
VERCMD:	CP	3
	SCF
	RET	NZ
	CALL	BLOCAD
	JR	VERF2
;
VERF1:	PUSH	BC
	EX	DE,HL
	CALL	LOAD
	EX	DE,HL
	LD	B,C
	CALL	LOAD
	LD	A,B
	CALL	CHECK
	POP	BC
	RET	NZ
	INC	HL
	INC	DE
	DEC	BC
VERF2:	LD	A,B
	OR	C
	JR	NZ,VERF1
	RET
;
BANKSW:	CP	2
	CCF
	RET	C
	OR	A
	JR	NZ,BANK2
	LD	A,(CONFIG)
	CALL	PUT2HS
	OR	A
	RET
;
BANK2:	LD	A,L
	LD	HL,CONFIG
	XOR	(HL)
	BIT	1,A
	JR	Z,BANK2A
	DI
	LD	A,(STDCPY)
	LD	E,A
	XOR	00001000B
	LD	D,A
	LD	HL,MONITR
	LD	BC,4096
	CALL	SHADOW
	JR	NZ,BANK3
	LD	A,D
	OUT	(PORT3),A
	LD	(STDCPY),A
	EI
BANK2A:	LD	A,(PARAM1)
	AND	00000011B
	LD	(CONFIG),A
	RET
;
BANK3:	EI
	CALL	PNEXT
	DEFB	'ERR AT ',EOT
	CALL	PUT4HS
	OR	A
	RET
;
SHADOW:	LD	A,(HL)
	EX	AF,AF'
	LD	A,D
	OUT	(PORT3),A
	EX	AF,AF'
	LD	(HL),A
	CP	(HL)
	LD	A,E
	OUT	(PORT3),A
	RET	NZ
	INC	HL
	DEC	BC
	LD	A,B
	OR	C
	JR	NZ,SHADOW
	RET
;
GETLIN:	LD	B,C
GLIN1:	CALL	ECHO
	CP	CR
	JR	Z,GLIN2
	CP	'H'-64
	JR	Z,GLIN4
	CP	' '
	RET	C
	LD	(HL),A
	INC	HL
	DEC	C
	JR	NZ,GLIN1
	SCF
	RET
;
GLIN2:	LD	(HL),A
	RET
;
GLIN4:	DEC	HL
	CALL	PNEXT
	DEFB	' ','H'-64,EOT
	INC	C
	LD	A,B
	SUB	C
	JR	NC,GLIN1
	RET
;
SEARCH:	CPIR
	RET	NZ
	ADD	HL,BC
	ADD	HL,BC
	ADD	HL,BC
	LD	A,(HL)
	INC	HL
	LD	H,(HL)
	LD	L,A
	RET
;
PARAMS:	LD	BC,0
	LD	A,(IY+0)
	CP	CR
	JR	NZ,PARA2
	XOR	A
	RET
;
PARA1:	INC	C
	INC	C
	LD	A,C
	CP	10
	CCF
	RET	C
PARA2:	PUSH	BC
	CALL	GETHEX
	POP	BC
	RET	C
	LD	IX,PARAM1
	ADD	IX,BC
	LD	(IX+0),L
	LD	(IX+1),H
	CP	' '
	JR	Z,PARA1
	CP	','
	JR	Z,PARA1
	CP	CR
	SCF
	RET	NZ
	LD	A,C
	SRL	A
	INC	A
	RET
;
GETHEX:	LD	HL,0
	JR	GNUM3
;
GNUM1:	LD	B,4
GNUM2:	ADD	HL,HL
	RET	C
	DJNZ	GNUM2
	LD	E,A
	LD	D,0
	ADD	HL,DE
	RET	C
GNUM3:	LD	A,(IY+0)
	INC	IY
	LD	C,A
	CALL	ASCHEX
	JR	NC,GNUM1
	LD	A,C
	OR	A
	RET
;
ASCHEX:	SUB	'0'
	RET	C
	CP	10
	CCF
	RET	NC
	SUB	7
	CP	10
	RET	C
	CP	16
	CCF
	RET
;
PUT4HS:	LD	A,H
	CALL	PUT2HX
	LD	A,L
PUT2HS:	CALL	PUT2HX
	CALL	SPACE
	RET
;
PUT2HX:	PUSH	AF
	RRA
	RRA
	RRA
	RRA
	CALL	PUTNIB
	POP	AF
PUTNIB:	AND	00001111B
	ADD	A,090H
	DAA
	ADC	A,040H
	DAA
	CALL	OUTPUT
	RET
;
EOT	EQU	04H
CR	EQU	0DH
LF	EQU	0AH
;
PNEXT:	EX	(SP),HL
	CALL	PMSG
	EX	(SP),HL
	RET
;
PMSG:	LD	A,(HL)
	INC	HL
	CP	EOT
	RET	Z
	CALL	OUTPUT
	JR	PMSG
;
CRLFS:	CALL	PNEXT
	DEFB	CR,LF,EOT
SPACE:	LD	A,' '
	CALL	OUTPUT
	RET
;
ECHO:	CALL	CONIN
	PUSH	AF
	CALL	CONOUT
	POP	AF
	CP	'Z'+1
	RET	C
	SUB	32
	RET
;
OUTPUT:	CALL	CONOUT
	CALL	CONST
	JR	Z,OUTP2
	CALL	CONIN
	CP	CR
	JR	Z,OUTP1
	CALL	CONIN
	JR	OUTP2
;
OUTP1:	LD	(BREAK),A
OUTP2:	LD	A,(BREAK)
	OR	A
	RET
;
MONCOPY	EQU	$
	.PHASE	MONITR
MONSTRT	EQU	$
;
COLD:	JP	REINIT
WARM:	JP	ROLLIN
CONST:	JP	KBDST
CONIN:	JP	KBDIN
CONOUT:	JP	VIDOUT
	JP	VIDOUT
	JP	SIOST
	JP	SIOIN
	JP	SIOOUT
	JP	SELECT
	JP	HOME
	JP	SEEK
	JP	READ
	JP	WRITE
	JP	READID
;
REINIT:	DI
	LD	A,DYSTAT+OFF
	OUT	(PORT0),A
	JP	0
;
ROLLIN:	DI
	LD	A,0
	OUT	(PORT0),A
	LD	(DSCOPY),A
	LD	SP,STACK
	EI
	JP	PROMPT
;
VIDOUT:	DI
	LD	(CRTSAV),SP
	LD	SP,CRTSTK
	PUSH	HL
	PUSH	DE
	PUSH	BC
	LD	C,A
	LD	A,(DSCOPY)
	PUSH	AF
	LD	A,DYSTAT+OFF
	LD	(DSCOPY),A
	OUT	(PORT0),A
	EI
	LD	HL,(CRTVEC)
	CALL	DISPATCH
	DI
	POP	AF
	LD	(DSCOPY),A
	OUT	(PORT0),A
	POP	BC
	POP	DE
	POP	HL
	LD	SP,(CRTSAV)
	EI
	RET
;
LOAD:	DI
	LD	A,(CONFIG)
	ADD	A,A
	ADD	A,A
	ADD	A,A
	OUT	(PORT0),A
	LD	C,(HL)
	LD	A,(DSCOPY)
	OUT	(PORT0),A
	EI
	RET
;
STORE:	DI
	LD	A,(CONFIG)
	ADD	A,A
	ADD	A,A
	ADD	A,A
	OUT	(PORT0),A
	LD	(HL),C
	LD	A,(DSCOPY)
	OUT	(PORT0),A
	EI
	RET
;
MOVE:	DI
	LD	A,(CONFIG)
	ADD	A,A
	ADD	A,A
	ADD	A,A
	OUT	(PORT0),A
	LDIR
	LD	A,(DSCOPY)
	OUT	(PORT0),A
	EI
	RET
;
EXITMON:
	DI
	PUSH	AF
	LD	A,(CONFIG)
	ADD	A,A
	ADD	A,A
	ADD	A,A
	AND	00001000B
	LD	(DSCOPY),A
	OUT	(PORT0),A
	POP	AF
	EI
	RET
;
MEMTEST:
	DI
	LD	A,(CONFIG)
	ADD	A,A
	ADD	A,A
	ADD	A,A
	OUT	(PORT0),A
MTST2:	LD	A,L
	XOR	H
	XOR	B
	LD	(HL),A
	INC	HL
	LD	A,H
	CP	E
	JR	NZ,MTST2
	LD	H,D
	LD	L,0
MTST3:	LD	A,L
	XOR	H
	XOR	B
	CP	(HL)
	JR	NZ,MTST4
	INC	HL
	LD	A,H
	CP	E
	JR	NZ,MTST3
MTST4:	LD	A,(DSCOPY)
	OUT	(PORT0),A
	EI
	RET
;
KBDST:	LD	A,(FIFCNT)
	OR	A
	RET	Z
	LD	A,255
	RET
;
KBDIN:	CALL	KBDST
	JR	Z,KBDIN
	PUSH	HL
	CALL	REMOVE
	POP	HL
	RET
;
STASH:	LD	C,A
	LD	A,(FIFSIZ)
	AND	00111111B
	LD	B,A
	LD	HL,FIFCNT
	LD	A,(HL)
	INC	A
	CP	B
	RET	NC
	LD	(HL),A
	LD	HL,FIFIN
	CALL	INDEX
	LD	(HL),C
	RET
;
REMOVE:	LD	HL,FIFCNT
	DEC	(HL)
      	LD	HL,FIFOUT
INDEX:	LD	A,(HL)
	INC	A
	AND	00111111B
	LD	(HL),A
	LD	HL,FIFO
	ADD	A,L
	LD	L,A
	LD	A,H
	ADC	A,0
	LD	H,A
	LD	A,(HL)
	RET
;
KEYSRV:	LD	(IRQSAV),SP
	LD	SP,IRQSTK
	PUSH	HL
	PUSH	DE
	PUSH	BC
	PUSH	AF
	IN	A,(KBD)
	LD	HL,(KBDVEC)
	CALL	DISPATCH
	POP	AF
	POP	BC
	POP	DE
	POP	HL
	LD	SP,(IRQSAV)
RETI:	EI
	RETI
;
MILLISEC:
	LD	(IRQSAV),SP
	LD	SP,IRQSTK
	PUSH	HL
	PUSH	DE
	PUSH	BC
	PUSH	AF
	LD	HL,(STPVEC)
	CALL	DISPATCH
	POP	AF
	POP	BC
	POP	DE
	POP	HL
	LD	SP,(IRQSAV)
	EI
	RETI
;
DISPATCH:
	JP	(HL)
;
TIMER:	LD	(IRQSAV),SP
	LD	SP,IRQSTK
	PUSH	HL
	PUSH	DE
	PUSH	BC
	PUSH	AF
	LD	HL,(TICKS)
	INC	HL
	LD	(TICKS),HL
	LD	HL,(TIKVEC)
	CALL	DISPATCH
	LD	HL,TIKCNT
	DEC	(HL)
	JR	NZ,TIMER2
	LD	A,(NTICKS)
	LD	(HL),A
	CALL	CLOCK
	CALL	DISKTEST
TIMER2:	POP	AF
	POP	BC
	POP	DE
	POP	HL
	LD	SP,(IRQSAV)
	EI
	RETI
;
VSYNC:	LD	(IRQSAV),SP
	LD	SP,IRQSTK
	PUSH	HL
	PUSH	DE
	PUSH	BC
	PUSH	AF
	LD	A,(DSCOPY)
	PUSH	AF
	LD	A,DYSTAT+OFF
	OUT	(PORT0),A
	LD	A,(SCROLL)
	OR	A
	JR	Z,VSYNC2
	LD	HL,(START)
	LD	A,12
	OUT	(CRTADD),A
	LD	A,H
	OUT	(CRTDAT),A
	LD	A,13
	OUT	(CRTADD),A
	LD	A,L
	OUT	(CRTDAT),A
	LD	DE,(NEWLIN)
	ADD	HL,DE
	LD	A,H
	AND	00000111B
	LD	H,A
	LD	BC,80
	CALL	CLRLINE
VSYNC2:	LD	HL,(OLDCSR)
	LD	A,(HL)
	BIT	7,A
	JR	Z,VSYNC3
	XOR	10010000B
	LD	(HL),A
VSYNC3:	LD	HL,(CURSOR)
	LD	DE,ATTMEM
	ADD	HL,DE
	LD	A,(HL)
	XOR	10010000B
	LD	(HL),A
	LD	(OLDCSR),HL
	LD	A,00000011B
	OUT	(CTCA3),A
	XOR	A
	LD	(SCROLL),A
	LD	(MOVECS),A
	POP	AF
	OUT	(PORT0),A
	POP	AF
	POP	BC
	POP	DE
	POP	HL
	LD	SP,(IRQSAV)
	EI
	RETI
;
SIOST:	IN	A,(SIOCPB)
	AND	00000001B
	RET	Z
	LD	A,255
	RET
;
SIOIN:	CALL	SIOST
	JR	Z,SIOIN
	IN	A,(SIODPB)
	AND	01111111B
	RET
;
SIOINT:	LD	(IRQSAV),SP
	LD	SP,IRQSTK
	PUSH	HL
	PUSH	DE
	PUSH	BC
	PUSH	AF
	LD	HL,SIOADR+1
	LD	C,(HL)
	IN	A,(C)
	AND	01111111B
	LD	HL,(RDAVEC)
	CALL	DISPATCH
	POP	AF
	POP	BC
	POP	DE
	POP	HL
	LD	SP,(IRQSAV)
	EI
	RETI
;
SIOERR:	LD	(IRQSAV),SP
	LD	SP,IRQSTK
	PUSH	AF
	PUSH	BC
	LD	A,(SIOADR)
	LD	C,A
	LD	A,00110000B
	OUT	(C),A
	LD	A,(SIOADR+1)
	LD	C,A
	IN	A,(C)
	POP	BC
	POP	AF
	EI
	RETI
;
SIOOUT:	PUSH	BC
	PUSH	AF
	LD	A,(SIOADR)
	LD	C,A
SIOUT1:	IN	A,(C)
	AND	00000100B
	JR	Z,SIOUT1
	LD	A,(SIOADR+1)
	LD	C,A
	POP	AF
	OUT	(C),A
	POP	BC
	RET
;
CLOCK:	LD	DE,TOD
	LD	HL,TODTAB
	LD	B,4
CLOCK2:	LD	A,(DE)
	ADD	A,1
	DAA
	CP	(HL)
	JR	C,CLOCK3
	XOR	A
CLOCK3:	LD	(DE),A
	RET	C
	INC	HL
	INC	DE
	DJNZ	CLOCK2
CLOCK4:	RET
;
TODTAB:	DEFB	60H
	DEFB	60H
	DEFB	24H
	DEFB	99H
;
STSREG	EQU	WD179X+0
CMDREG	EQU	WD179X+0
TRKREG	EQU	WD179X+1
SECREG	EQU	WD179X+2
DATREG	EQU	WD179X+3
;
RDCMD	EQU	10001000B
RIDCMD	EQU	11000000B
WRTCMD	EQU	10101000B
SKCMD	EQU	00011100B
RSTCMD	EQU	00001000B
FINCMD	EQU	11010000B
STEPOUT	EQU	01100000B
STEPIN	EQU	01000000B
;
SELECT:	LD	A,C
	CP	4
	RET	NC
	CALL	DSEL
	CALL	READY
	JR	NZ,SELX
	LD	HL,UNIT
	LD	D,0
	LD	E,(HL)
	LD	(HL),C
	LD	HL,TRKTAB
	ADD	HL,DE
	LD	A,(TRACK)
	LD	(HL),A
	LD	E,4
	ADD	HL,DE
	LD	A,(DSKTYP)
	AND	00000001B
	LD	(HL),A
	LD	HL,TRKTAB
	LD	E,C
	ADD	HL,DE
	LD	A,(HL)
	OUT	(TRKREG),A
	LD	(TRACK),A
	LD	E,4
	ADD	HL,DE
	LD	A,(DSKTYP)
	AND	11111110B
	OR	(HL)
	CALL	SETDENS
	XOR	A
SELX:	CALL	SETTIMER
	RET
;
DSEL:	LD	HL,SELTAB
	AND	00000011B
	LD	D,0
	LD	E,A
	ADD	HL,DE
	LD	A,(HL)
	LD	(SELCPY),A
	RET
;
SELTAB:	DEFB	10001010B
	DEFB	01001010B
	DEFB	00101010B
	DEFB	00011010B
;
HOME:	LD	C,0
SEEK:	CALL	READY
	JR	NZ,SEEKX
	LD	A,(TRACK)
	SUB	C
	JR	Z,SEEKX
	LD	A,(TRACK)
	CP	255
	JR	NZ,SEEK2
	LD	A,RSTCMD+3
	CALL	DISKOP
	XOR	00000100B
	AND	10000101B
	JR	NZ,SEEKX
SEEK2:	LD	B,A
	CALL	FINDTRK
	PUSH	AF
	LD	A,C
	LD	(TRACK),A
	OUT	(TRKREG),A
	POP	AF
SEEKX:	CALL	SETTIMER
	RET
;
FINDTRK:
	LD	A,7
	LD	(FNDTRY),A
FTRK2:	PUSH	BC
	CALL	STEP
	POP	BC
	JR	NZ,FTRK4
	IN	A,(SECREG)
	LD	B,A
	SUB	C
	RET	Z
	LD	A,(FNDTRY)
	CP	7
	JR	Z,FTRK3
	LD	HL,SPEED
	INC	(HL)
FTRK3:	DEC	A
	LD	(FNDTRY),A
	JR	NZ,FTRK2
	LD	A,00010000B
	OR	A
FTRK4:	LD	C,255
	RET
;
STEP:	LD	D,STEPOUT
	LD	A,B
	SUB	C
	JR	NC,STEP2
	LD	D,STEPIN
	LD	A,C
	SUB	B	
STEP2:	JR	Z,STEP4
	LD	(STPCNT),A
	LD	A,D
	LD	(STPCMD),A
	LD	A,001H
	LD	(STPDLY),A
	LD	HL,DOSTEP
	LD	(STPVEC),HL
	LD	A,(SELCPY)
	RES	3,A
	OUT	(SELMUX),A
	LD	A,10000001B
	OUT	(CTCB2),A
STEP3:	LD	A,(STPCNT)
	OR	A
	JR	NZ,STEP3
	LD	A,(SELCPY)
	SET	3,A
	OUT	(SELMUX),A
STEP4:	CALL	VERIFY
STEPX:	RET
;
DOSTEP:	LD	HL,STPDLY
	DEC	(HL)
	RET	NZ
	LD	A,(STPCMD)
	OUT	(CMDREG),A
	LD	A,(STPCNT)
	DEC	A
	JR	Z,DOSTP2
	LD	(STPCNT),A
	LD	A,(SPEED)
	LD	(HL),A
	RET
;			
DOSTP2:	LD	A,(SETTLE)
	LD	(HL),A
	LD	HL,DOSETTLE
	LD	(STPVEC),HL
	RET
;
DOSETTLE:
	LD	HL,STPDLY
	DEC	(HL)
	RET	NZ
	XOR	A
	LD	(STPCNT),A
	LD	HL,SEEKX
	LD	(STPVEC),HL
	LD	A,00000001B
	OUT	(CTCB2),A
	RET
;
VERIFY:	LD	A,RIDCMD
	CALL	DISKOP
	AND	10011001B
	JR	Z,VERFY2
	CALL	FLIPDENS
	LD	A,RIDCMD
	CALL	DISKOP
	AND	10011001B
VERFY2:	PUSH	AF
	CALL	FORCE
	POP	AF
	RET	Z
	PUSH	AF
	CALL	FLIPDENS
	POP	AF
	RET
;
FLIPDENS:
	LD	A,(DSKTYP)
	XOR	00000001B
SETDENS:
	LD	(DSKTYP),A
	AND	00000011B
	LD	B,0
	LD	C,A
	LD	HL,SMCTAB
	ADD	HL,BC
	ADD	HL,BC
	ADD	HL,BC
	LD	B,3
	LD	C,PORT0
	OTIR
	RET
;
SMCTAB:	DEFB	SMC1+ON
	DEFB	SMC2+OFF
	DEFB	DDEN+ON
	DEFB	SMC1+OFF
	DEFB	SMC2+OFF
	DEFB	DDEN+OFF
	DEFB	SMC1+OFF
	DEFB	SMC2+ON
	DEFB	DDEN+ON
	DEFB	SMC1+ON
	DEFB	SMC2+OFF
	DEFB	DDEN+OFF
;
READID:	CALL	READY
	JR	NZ,RDIDX
	LD	B,RIDCMD
	CALL	RDWRT
	JR	NZ,RDIDX
	LD	HL,(IOPTR)
	LD	DE,6
	ADD	HL,DE
	LD	A,(DSKTYP)
	LD	(HL),A
	XOR	A
RDIDX:	CALL	SETTIMER
	RET
;
WRITE:	CALL	READY
	JR	NZ,WRITEX
	BIT	6,A
	JR	NZ,WRITEX
	LD	B,WRTCMD
	CALL	RDWRT
WRITEX:	CALL	SETTIMER
	RET
;
READ:	CALL	READY
	JR	NZ,READX
	LD	B,RDCMD
	CALL	RDWRT
READX:	CALL	SETTIMER
	RET
;
RDWRT:	LD	(IOPTR),HL
	LD	A,C
	LD	(SECTOR),A
	LD	A,B
	LD	(CMDTYP),A
	LD	A,(MAXRWT)
	LD	(RWTRY),A
RW1:	LD	A,(SECTOR)
	OUT	(SECREG),A
	LD	HL,DMAPGM
	LD	B,8
	LD	C,DMA
	OTIR
	LD	DE,(BLKSIZ)
	OUT	(C),E
	OUT	(C),D
	LD	B,3
	OTIR
	LD	DE,(IOPTR)
	OUT	(C),E
	OUT	(C),D
	OUTI
	LD	B,4
	LD	A,(CMDTYP)
	CP	WRTCMD
	JR	Z,RW2
	INC	HL
	INC	HL
	LD	B,2
RW2:	OTIR
	CALL	DISKOP
	LD	B,4
	OTIR
	IN	L,(C)
	IN	H,(C)
	AND	10011111B
	RET	Z
	LD	(ERRTYP),A
	CALL	RECOVER
	JR	NZ,RW3
	LD	HL,RWTRY
	DEC	(HL)
	JR	NZ,RW1
RW3:	LD	A,(ERRTYP)
	OR	A
	RET
;
RECOVER:
	LD	B,A
	AND	10000111B
	JR	Z,RECOV1
	CALL	FORCE
	LD	A,B
	OR	A
	RET
;
RECOV1:	BIT	4,B
	JR	NZ,RECOV3
	LD	A,(RWTRY)
	LD	HL,MAXRWT
	SUB	(HL)
	RET	Z
	IN	A,(TRKREG)
	LD	B,A
	OR	A
	JR	NZ,RCOV2A
	LD	C,1
	JR	RCOV2B
;
RCOV2A:	DEC	A
	LD	C,A
RCOV2B:	PUSH	BC
	CALL	STEP
	POP	DE
	LD	B,E
	LD	C,D
	CALL	STEP
	RET
;
RECOV3:	LD	A,(DSKTYP)
	PUSH	AF
	CALL	VERIFY
	POP	DE
	RET	NZ
	LD	A,(CMDTYP)
	CP	RIDCMD
	JR	Z,RECOV4
	IN	A,(SECREG)
	LD	B,A
	IN	A,(TRKREG)
	LD	C,A
	CP	B
	JR	Z,RECOV4
	CP	255
	JR	Z,RECOV5
	CALL	FINDTRK
	PUSH	AF
	LD	A,C
	OUT	(TRKREG),A
	POP	AF
	RET
;
RECOV4:	LD	A,(DSKTYP)
	CP	D
	JR	Z,RECOV5
	XOR	A
	RET
;
RECOV5:	LD	A,00010000B
	OR	A
	RET
;
DMAPGM:	DEFB	11000011B
	DEFB	11000011B
	DEFB	11000011B
	DEFB	11000011B
	DEFB	11000011B
	DEFB	11000011B
	DEFB	01101101B
	DEFB	DATREG

	DEFB	00101100B
	DEFB	00010000B
	DEFB	10001101B

	DEFB	10001010B

	DEFB	11001111B
	DEFB	00000001B
	DEFB	11001111B
	DEFB	10000111B

	DEFB	10000011B
	DEFB	10111011B
	DEFB	00000110B
	DEFB	10100111B

DISKOP:
	CALL	CMDOUT
	XOR	A
	LD	(RDYCNT),A
DSKOP2:	IN	A,(STSREG)
	BIT	0,A
	RET	Z
	LD	A,(RDYCNT)
	OR	A
	JR	Z,DSKOP2
	CALL	FORCE
	LD	A,00000001B
	OR	A
	RET
;
CMDOUT:	OUT	(CMDREG),A
	LD	A,12
COUT2:	DEC	A
	JR	NZ,COUT2
	RET
;
FORCE:	LD	A,FINCMD
	CALL	CMDOUT
	IN	A,(STSREG)
	RET
;
READY:	LD	A,1
	LD	(INUSE),A
	LD	A,(SELCPY)
	OUT	(SELMUX),A
	LD	A,(DSKCNT)
	OR	A
	JR	Z,READY2
	CALL	FORCE
	BIT	7,A
	RET	Z
READY2:	CALL	SPINUP
	RET	NZ
	CALL	FORCE
	BIT	7,A
	RET
;
SETTIMER:
	PUSH	AF
	LD	A,(NSTOP)
	LD	(DSKCNT),A
	XOR	A
	LD	(INUSE),A
	POP	AF
	RET
;
SPINUP:	PUSH	BC
	LD	A,MOTOR+ON
	OUT	(PORT0),A
	LD	A,HLDTIM+ON
	OUT	(PORT0),A
	IN	A,(CTCA1)
	LD	C,A
	LD	A,(NREVS)
	LD	B,A
	XOR	A
	LD	(RDYCNT),A
	CALL	FORCE
SPIN2:	LD	A,(RDYCNT)
	CP	4
	JR	NC,SPIN4
	IN	A,(STSREG)
	BIT	7,A
	JR	NZ,SPIN2
	IN	A,(CTCA1)
	SUB	C
	NEG
	CP	B
	JR	C,SPIN2
	XOR	A
	JR	SPIN5
;
SPIN4:	LD	A,10000000B
SPIN5:	POP	BC
	OR	A
	RET
;
DISKTEST:
	LD	HL,OLDCTC
	IN	A,(CTCA1)
	LD	C,(HL)
	LD	(HL),A
	LD	A,(INUSE)
	OR	A
	JR	Z,DTST2
	LD	A,(HL)
	CP	C
	RET	NZ
	LD	HL,RDYCNT
	INC	(HL)
	RET
;
DTST2:	LD	A,C
	SUB	(HL)
	JR	Z,DTST3
	LD	B,A
	LD	A,(DSKCNT)
	SUB	B
	JR	C,DTST3
	JR	Z,DTST3
	LD	(DSKCNT),A
	RET
;
DTST3:	LD	A,1
	LD	(INUSE),A
	XOR	A
	LD	(DSKCNT),A
	LD	A,MOTOR+OFF
	OUT	(PORT0),A
	LD	A,HLDTIM+OFF
	OUT	(PORT0),A
	LD	A,(SELCPY)
	AND	00001111B
	OUT	(SELMUX),A
	RET
;
MONLEN	EQU	$-MONSTRT
MONEND	EQU	$
	.DEPHASE
;
RAMCOPY	EQU	$
	.PHASE	RAM
RAMSTRT	EQU	$
;
SIOVEC:	DEFW	RETI		;SIOB TX INTERRUPT
	DEFW	RETI		;SIOB EXTERNAL/STATUS INTERRUPT
	DEFW	SIOINT		;SIOB RX INTERRUPT
	DEFW	SIOERR		;SIOB SPECIAL RX CONDITION INTERRUPT
	DEFW	RETI		;SIOA TX INTERRUPT
	DEFW	RETI		;SIOA EXTERNAL/STATUS INTERRUPT
	DEFW	SIOINT		;SIOA RX INTERRUPT
	DEFW	SIOERR		;SIOA SPECIAL RX CONDITION INTERRUPT

CTCAV:	DEFW	KEYSRV		;CTCA0 INTERRUPT (KBD STROBE)
	DEFW	RETI
	DEFW	RETI
	DEFW	VSYNC		;CTCA3 INTERRUPT (CRT VERT SYNC)

CTCBV:	DEFW	RETI
	DEFW	RETI	
	DEFW	MILLISEC	;CTCB2 INTERRUPT (MILLISECOND TIMER)
	DEFW	TIMER		;CTCB3 INTERRUPT (CLOCK TICK)

DMAVEC:	DEFW	RETI		;DMA READY INTERRUPT
	DEFW	RETI		;DMA MATCH INTERRUPT
	DEFW	RETI		;DMA END-OF-BLOCK INTERRUPT
	DEFW	RETI		;DMA MATCH/END INTERRUPT

	DEFW	0,0,0,0,0,0,0,0	;EXTRA INTERRUPT VECTORS
	DEFW	0,0,0,0,0,0,0,0
	DEFW	0,0,0,0,0,0,0,0
	DEFW	0,0,0,0,0,0,0,0
;
;
;	KEYBOARD DATA INPUT FIFO VARIABLES

FIFCNT:	DEFB	0		;FIFO DATA COUNTER
FIFIN:	DEFB	0		;FIFO INPUT POINTER
FIFOUT:	DEFB	0		;FIFO OUTPUT POINTER
FIFSIZ:	DEFB	32		;MAX FIFO SIZE PARAM
;
;
CONNUM:	DEFB	0		;CURRENT CONSOLE DEVICE# (0,1 OR 2)
SIOADR:	DEFB	0		;CONSOLE SIO CONTROL/DATA PORT NUMBER
	DEFB	0
;
;
CONFIG:	DEFB	00000001B	;STDBB AND D/S BITS FOR MONITOR
DSCOPY:	DEFB	DYSTAT+OFF	;CONTROL BYTE FOR D/S BANK SWITCH BIT
STDCPY:	DEFB	STDBB+OFF	;CONTROL BYTE FOR STD BANK SWITCH BIT
;
;
;	VECTORS FOR EXTRA FUNCTIONS IN KBD/SIO/TIMER INTERRUPTS
;	AND FOR EXTENDED MONITOR AND CRTOUT ROUTINES.
;
KBDVEC:	DEFW	STASH		;PARALLEL KEYBOARD
RDAVEC:	DEFW	STASH		;SIO RECEIVED DATA INTERRUPT
TIKVEC:	DEFW	CLOCK4		;ONE-SECOND INTERRUPT
MONVEC:	DEFW	EXT2		;EXTENDED MONITOR
CRTVEC:	DEFW	CRTOUT		;CRT OUTPUT DRIVER
;
;
;	CLOCK-TIMER INTERRUPT VARIABLES

TOD	EQU	$
SECS:	DEFB	0		;CLOCK SECONDS
MINS:	DEFB	0		;      MINUTES
HRS:	DEFB	0		;      HOURS
DAY:	DEFB	0		;CALENDAR DAY
MONTH:	DEFB	0		;	  MONTH
YEAR:	DEFB	0		;	  YEAR
;
TICKS:	DEFW	0		;CLOCK TICK INTERRUPT COUNTER
NTICKS:	DEFB	4		;NUMBER OF TICKS PER SECOND
TIKCNT:	DEFB	1		;PRESCALER FOR 1 SECOND TIMER
;
;
;	DISK I/O DRIVER VARIABLES

UNIT:	DEFB	255		;CURRENTLY SELECTED DISK#
TRACK:	DEFB	255		;TRACK POSITION OF SELECTED DRIVE
TRKTAB:	DEFB	255,255,255,255	;HEAD POSITIONS FOR 4 DRIVES
	DEFB	0,0,0,0		;DENSITY CONTROL BITS FOR 4 DRIVES
SPEED:	DEFB	6		;SEEK SPEED FOR 1771 COMMANDS
SETTLE:	DEFB	15		;HEAD SETTLING TIME
BLKSIZ:	DEFW	1024		;MAX DMA BLOCK SIZE FOR READ/WRITE
NSTOP:	DEFB	9		;NUMBER OF INDEX PULSES TILL DISK STOP
NREVS:	DEFB	3		;NUMBER OF INDEX PULSES BEFORE READY
DSKTYP:	DEFB	00000001B	;DISK TYPE / DENSITY INDICATOR
MAXRWT:	DEFB	5		;MAXIMUM RETRY NUMBER FOR DISK I/O

SECTOR:	DEFB	0		;SECTOR# FOR READ/WRITE SECTOR
CMDTYP:	DEFB	0		;COMMAND BYTE FOR READS/WRITES
RWTRY:	DEFB	0		;READ/WRITE RETRY COUNT
FNDTRY:	DEFB	0		;SEEK RETRY COUNT
ERRTYP:	DEFB	0		;ERROR STATUS FOR READ/WRITE ROUTINES
IOPTR:	DEFW	0		;DISK I/O BUFFER POINTER
SELCPY:	DEFB	0		;COPY OF DATA IN SELECT/MUX OUTPUTS
STPVEC:	DEFW	STEPX		;VECTOR FOR STEP FINITE STATE MACHINE
STPCMD:	DEFB	0		;... 179X STEP COMMAND BYTE
STPCNT:	DEFB	0		;... LOOP COUNT
STPDLY:	DEFB	0		;... DELAY COUNT
INUSE:	DEFB	0		;DISKS IN-USE FLAG FOR BACKGROUND
DSKCNT:	DEFB	0		;... DISK TURN-OFF COUNTER
RDYCNT:	DEFB	0		;... NOT-READY TIMER
OLDCTC:	DEFB	0		;... LAST CTCA1 COUNT
;
;
;
;	CRT OUTPUT DRIVER VARIABLES

RC	EQU	$
ROW:	DEFB	0		;ROW/COLUMN FOR CRT CURSOR LOCATION
COL:	DEFB	0
BLANK:	DEFB	' '		;CHARACTER USED FOR BLANK FILL
ATTRIB:	DEFB	00000011B	;CURRENT CHARACTER ATTRIBUTE BITS
GRMODE:	DEFB	00000000B	;CURRENT DISPLAY MODE BITS
ESCVEC:	DEFW	0		;POINTER FOR LEAD-IN SEQUENCE ROUTINES
ROWTMP:	DEFB	0
SCROLL:	DEFB	0		;SCROLL REQUEST FLAG
START:	DEFW	0		;CONTENTS OF 6845 START ADDRESS REG
NEWLIN:	DEFW	0		;RELATIVE ADDR OF NEW LINE AFTER SCROLL
MOVECS:	DEFB	0		;CURSOR-MOVE REQUEST FLAG
CURSOR:	DEFW	0		;OFFSET (0..2047) TO CURSOR LOCATION
OLDCSR:	DEFW	ATTMEM		;POINTER TO CURSOR IN ATTR MEMORY
CRTFLG:	DEFB	00000001B	;CRT PARAMETER BITS

AUTONL	EQU	0
NOSCRL	EQU	1
DSPTIM	EQU	2

;
;	FREE MEMORY LIST POINTER
;

FREPTR:	DEFW	TAIL
;
RAMLEN	EQU	$-RAMSTRT
	.DEPHASE
;
	.PHASE	MONEND
IRQSAV:	DEFS	2
	DEFS	24
IRQSTK	EQU	$
;
CRTSAV:	DEFS	2
	DEFS	24
CRTSTK	EQU	$
;
PARAM1:	DEFS	2
PARAM2:	DEFS	2
PARAM3:	DEFS	2
PARAM4:	DEFS	2
PARAM5:	DEFS	2
BREAK:	DEFS	1
LAST:	DEFS	2
LENGTH:	DEFS	2
LINBUF:	DEFS	64
LINSIZ	EQU	$-LINBUF
	DEFS	32
STACK	EQU	$
;
FIFO:	DEFS	64
;
TAIL	EQU	$
	.DEPHASE
;
	END
