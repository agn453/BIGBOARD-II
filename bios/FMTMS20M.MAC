;
;	F M T M S - 2 0
;
;	FORMAT MINISCRIBE 3425 HARD DISK ON THE SHUGART 1610-3 CONTROLLER
;
;	Author:		Tony Nicholson
;	Date:		03-Dec-1987
;
;
	.Z80
;
FALSE	EQU	0
TRUE	EQU	NOT FALSE
;
S512	EQU	TRUE		;TRUE FOR 512 BYTE SECTORS
;
BDOS	EQU	00005H
CR	EQU	00DH
LF	EQU	00AH
;
SELCPY	EQU	0FF9FH
INUSE	EQU	0FFA5H
;
DMA	EQU	08CH		;Z80 DMA PORT ADDRESS
SELMUX	EQU	0CCH		;DRIVE SEL/READY MUX CONTROL PORT
SASI	EQU	0D8H		;SASI BUS BASE ADDRESS
SASI_S	EQU	SASI+1

;
;  SASI STATUS BIT DEFINITIONS
;
CD	EQU	00100000B	;C/D BIT MASK
CMD	EQU	00100000B	;  COMMAND STATUS
DATA	EQU	00000000B	;  DATA STATUS

MSG	EQU	00001000B	;MSG BIT MASK
MESSAG	EQU	00001000B	;  MESSAGE STATUS

BUSY	EQU	00000010B	;BUSY BIT MASK

IO	EQU	00000001B	;I/O BIT MASK
INPUT	EQU	00000001B	;  WAITING FOR INPUT
OUTPUT	EQU	00000000B	;  WAITING FOR OUTPUT 

CHKSTS	EQU	00000010B	;CHECK STATUS MASK
ERROR	EQU	00000010B	;  ERROR

SEL	EQU	3
RST	EQU	2
ON	EQU	00001000B
OFF	EQU	00000000B
;
FORMAT::
	LD	A,RST+ON
	OUT	(SASI_S),A	;RESET SASI BUS CONTROLLER(S)
	LD	B,25
RSTDLY:	DJNZ	RSTDLY		;  RST ACTIVE FOR > 25 uSEC
	LD	A,RST+OFF
	OUT	(SASI_S),A

	LD	HL,CHKMS
	CALL	PMSG

	LD	HL,DCBE4	;PERFORM CONTROLLER DIAGNOSTIC
	CALL	DOCMD
	CALL	STATUS
	JP	NZ,CTRLER	;ERROR DETECTED
	
	LD	HL,DCB0C	;SPECIFY DRIVE
	CALL	DOCMD

	LD	HL,DRVPRM
OUT8:	IN	A,(SASI_S)
	AND	CD+IO
	CP	DATA+OUTPUT
	JP	NZ,OUT8
	LD	B,8
	LD	C,SASI
	CALL	REQ
	OTIR			;OUTPUT DATA

	CALL	STATUS
	JP	NZ,SPCER

	LD	HL,DCB0F	;WRITE SECTOR BUFFER
	CALL	DOCMD
	LD	HL,SECTBF
	CALL	OUTSEC
	CALL	STATUS
	JP	NZ,CTRLER	;ERROR DETECTED

	LD	HL,DCB10	;READ SECTOR BUFFER
	CALL	DOCMD
	LD	HL,RDBUFF
	CALL	INPSEC
	CALL	STATUS
	JP	NZ,CTRLER	;ERROR DETECTED

	LD	HL,SECTBF	;NOW VERIFY CONTROLLER
	LD	DE,RDBUFF	; RAM
  IF S512
	LD	BC,512
  ELSE
	LD	BC,256
  ENDIF
VERNXT::
	LD	A,(DE)
	CP	(HL)
	JP	NZ,CTRLER
	INC	HL
	INC	DE
	DEC	BC
	LD	A,B
	OR	C
	JP	NZ,VERNXT

	LD	HL,FMTMS
	CALL	PMSG

	LD	HL,DCB00	;CHECK DRIVE READY
	CALL	DOCMD
	CALL	STATUS
	JP	NZ,NREADY	;ERROR DETECTED
	
	LD	HL,DCB01	;SEEK TO TRACK 0
	CALL	DOCMD
	CALL	STATUS
	JP	NZ,SEEKER	;ERROR DETECTED

	LD	HL,DCB04	;FORMAT DRIVE
	CALL	DOCMD
	CALL	STATUS
	JP	NZ,FMTER	;ERROR DETECTED

	LD	HL,VERMS
	CALL	PMSG

	LD	HL,DCBE3	;DRIVE DIAGNOSTIC
	CALL	DOCMD
	CALL	STATUS
	JP	NZ,FMTER

	LD	HL,DCB08	;READ A SECTOR
	CALL	DOCMD
	LD	HL,RDBUFF
	CALL	INPSEC
	CALL	STATUS
	JP	NZ,CTRLER	;ERROR DETECTED

	LD	HL,DONEMS
	CALL	PMSG
	JP	0

CTRLER::
	LD	HL,CTRLMS
	CALL	PMSG
	JP	0

CTRLMS:	DEFB	CR,LF,'CONTROLLER ERROR',0

NREADY::
	LD	HL,DNRMS
	CALL	PMSG
	JP	SENSE

DNRMS:	DEFB	CR,LF,'DRIVE NOT READY',0

SPCER::
	LD	HL,SPCEMS
	CALL	PMSG
	JP	SENSE

SPCEMS:	DEFB	CR,LF,'DRIVE SPECIFY ERROR',0

FMTER::
	LD	HL,FMTEMS
	CALL	PMSG
	JP	SENSE

FMTEMS:	DEFB	CR,LF,'FORMAT ERROR',0

SEEKER::
	LD	HL,SEEKMS
	CALL	PMSG
	JP	SENSE

SEEKMS:	DEFB	CR,LF,'SEEK ERROR',0

;
;
CHKMS:	DEFB	CR,LF,'PERFORMING CONTROLLER CHECKOUT...',0

FMTMS:	DEFB	CR,LF,'FORMATTING DRIVE...',0

VERMS:	DEFB	CR,LF,'VERIFYING FORMAT...',0

DONEMS:	DEFB	CR,LF,'DONE.',0

;
;
SENSE::
	LD	HL,DCB03
	CALL	DOCMD

SENSE1:	IN	A,(SASI_S)	;WAIT FOR STATUS
	AND	CD+IO
	CP	DATA+INPUT
	JP	NZ,SENSE1

	CALL	REQ
	LD	HL,ERRBUF
	LD	B,4
	LD	C,SASI
	INIR

	LD	HL,SASMSG
	CALL	PMSG		;PRINT SASI BUS ERROR MESSAGE
	LD	A,(ERRBUF)
	BIT	7,A		;TEST IF ADDRESS FIELD IS APPLICABLE
	JP	Z,DWINX		;PRINT ONLY FIRST STATUS BYTE IF NOT
	RES	7,A
	CALL	PUTHEX
	LD	A,(ERRBUF+1)
	CALL	PUTHEX
	LD	A,(ERRBUF+2)
	CALL	PUTHEX
	LD	A,(ERRBUF+3)
DWINX:	CALL	PUTHEX

	JP	0

ERRBUF::
	DEFB	0,0,0,0

;
;
PUTHEX:
	PUSH	AF
	RRA
	RRA
	RRA
	RRA
	CALL	PNYBBL		;PRINT HIGH NYBBLE OF HEX BYTE
	POP	AF
	CALL	PNYBBL		;PRINT LOW NYBBLE OF HEX BYTE
	CALL	PMSG		;PRINT DELIMITER CHARACTER @HL
	RET
;
;
PNYBBL:	AND	00001111B
	ADD	A,90H
	DAA
	ADC	A,40H
	DAA
	LD	C,A
	CALL	OVEC
	RET
;
;
SASMSG:	DEFB	CR,LF
	DEFB	'SASI CONTROLLER STATUS IS ',0
	DEFB	'.',0
	DEFB	'.',0
	DEFB	'.',0
	DEFB	' ',0
;
;


DOCMD::
	PUSH	HL
	XOR	A
	LD	(EXTRA),A
	CALL	SELSASI
WAIT0:	CALL	READY
	JP	Z,WAIT0

WAIT1:	IN	A,(SASI_S)	;WAIT FOR COMMAND REQUEST
	AND	CD+IO
	CP	CMD+OUTPUT
	JP	Z,DOCMD1
	CP	CMD+INPUT
	JP	NZ,WAIT1
	IN	A,(SASI)
	LD	A,(EXTRA)
	INC	A
	LD	(EXTRA),A
	JP	WAIT1

DOCMD1:	CALL	DESELCT

	CALL	REQ
	POP	HL		;OUTPUT COMMAND TO CONTROLLER
	LD	B,6
	LD	C,SASI
	OTIR

	RET

EXTRA::
	DEFB	0

;
;
REQ::
	PUSH	HL
	DI
	LD	HL,INUSE
	LD	A,(HL)
	LD	(HL),1
	EI
	PUSH	AF
	OR	A		;TEST IF FLOPPYS WERE SELECTED OR NOT
	LD	A,(SELCPY)
	JP	Z,REQ1
	AND	00001111B
REQ1::	PUSH	AF
	OR	00001111B	;SELECT SASI REQ/ACK HANDSHAKE FLIPFLOP
	OUT	(SELMUX),A	; THROUGH DMA READY MULTIPLEXOR
	LD	A,10001010B
	OUT	(DMA),A		;PROGRAM DMA READY ACTIVE HIGH
REQ2::	LD	A,10111111B
	OUT	(DMA),A		;ISSUE READ-DMA-STATUS COMMAND
	IN	A,(DMA)
	BIT	1,A
	JP	NZ,REQ2		;LOOP TILL READY IS ACTIVE
	POP	AF
	OUT	(SELMUX),A
	POP	AF
	LD	(INUSE),A	;RESTORE IN-USE FLAG TO PREVIOUS STATE
	POP	HL
	RET
;
;
STATUS::
WAIT2:	IN	A,(SASI_S)	;WAIT FOR STATUS
	AND	CD+IO
	CP	CMD+INPUT
	JP	NZ,WAIT2

	CALL	REQ
	IN	A,(SASI)	;READ STATUS BYTE
	
	PUSH	AF
WAIT3:	IN	A,(SASI_S)
	AND	CD+MSG+IO
	CP	CMD+MESSAG+INPUT
	JP	NZ,WAIT3
	CALL	REQ
	IN	A,(SASI)
WAIT4:	IN	A,(SASI_S)
	AND	MSG
	CP	MESSAG
	JP	Z,WAIT4
	POP	AF
	AND	CHKSTS		;RETURN ERROR STATUS
	RET
;
;
INPSEC::
	IN	A,(SASI_S)
	AND	CD+IO
	CP	DATA+INPUT
	JP	NZ,INPSEC
	LD	BC,SASI
	CALL	REQ
	INIR			;INPUT DATA
  IF S512
	INIR
  ENDIF
	RET
;
;
OUTSEC::
	IN	A,(SASI_S)
	AND	CD+IO
	CP	DATA+OUTPUT
	JP	NZ,OUTSEC
	LD	BC,SASI
	CALL	REQ
	OTIR			;OUTPUT DATA
  IF S512
	OTIR
  ENDIF
	RET
;
;
DCB00::	DEFB	00000000B	;TEST UNIT READY COMMAND
	DEFB	00000000B
	DEFB	00
	DEFB	00
	DEFB	00
	DEFB	00
;
;
DCB01::	DEFB	00000001B	;REZERO UNIT COMMAND
	DEFB	00000000B
	DEFB	00
	DEFB	00
	DEFB	00
	DEFB	00		; (NO RETRY)
;
;
DCB03::	DEFB	00000011B	;REQUEST SENSE COMMAND
	DEFB	00000000B
	DEFB	00
	DEFB	00
	DEFB	00
	DEFB	00
;
;
DCB04::	DEFB	00000100B	;FORMAT DRIVE COMMAND
	DEFB	00000000B
	DEFB	00
	DEFB	00
	DEFB	03		;INTERLEAVE OF 3
	DEFB	10111000B	;NO RETRY, USE BUFFER DATA, 40uSEC STEP
;
;
DCB08::	DEFB	00001000B	;READ SECTOR COMMAND
	DEFB	00
BLKNHI::
	DEFB	00
BLKNLO::
	DEFB	00
NUMBLK::
	DEFB	01
	DEFB	00000000B
;
;
DCB0C::
	DEFB	00001100B	;SET DRIVE PARAMETERS
	DEFB	00
	DEFB	00
	DEFB	00
	DEFB	00
	DEFB	00
;
;
DRVPRM::
	DEFB	HIGH 615	;NUMBER OF CYLINDERS
	DEFB	LOW  615
	DEFB	04		;NUMBER OF HEADS
	DEFB	HIGH 615	;REDUCE WRITE CURRENT CYLINDER
	DEFB	LOW  615
	DEFB	HIGH 615	;WRITE PRECOMPENSATION CYLINDER
	DEFB	LOW  615
	DEFB	08		;MAXIMUM ECC BURST
;
;
DCB0F::
	DEFB	00001111B	;WRITE SECTOR BUFFER COMMAND
	DEFB	00
	DEFB	00
	DEFB	00
	DEFB	00
	DEFB	00
;
;
DCB10::
	DEFB	00010000B	;READ SECTOR BUFFER COMMAND
	DEFB	00
	DEFB	00
	DEFB	00
	DEFB	00
	DEFB	00
;
;
DCBE3::	DEFB	11100011B	;DRIVE DIAGNOSTIC
	DEFB	00
	DEFB	00
	DEFB	00
	DEFB	00
	DEFB	00
;
;
DCBE4::	DEFB	11100100B	;CONTROLLER DIAGNOSTIC
	DEFB	00000000B
	DEFB	00
	DEFB	00
	DEFB	00
	DEFB	00
;
;
READY::	PUSH	BC
	LD	BC,0
RDY0:	IN	A,(SASI_S)	;TEST IF SASI BUS IS READY
	AND	BUSY
	JP	NZ,RDY1		;IT IS READY, RETURN
	DEC	BC
	LD	A,B
	OR	C
	JP	NZ,RDY0
	PUSH	HL		;TIME-OUT, PRINT MESSAGE
	LD	HL,NRDYMS
	CALL	PMSG
	POP	HL
	LD	A,BUSY
	CP	BUSY		;RETURN Z-FLAG SET
RDY1:	POP	BC
	RET
;
NRDYMS:	DEFM	CR,LF,'SASI DOES NOT RESPOND BUSY',0
;
;
SELSASI::
	LD	A,00000001B	;SELECT CONTROLLER #1
	OUT	(SASI),A

	LD	A,SEL+ON
	OUT	(SASI_S),A
	LD	B,50
SELDLY:	DJNZ	SELDLY		;  SEL ACTIVE FOR > 50 uSEC
	RET
;
;
DESELCT::
	LD	A,SEL+OFF
	OUT	(SASI_S),A
	RET
;
;
PMSG::
	LD	A,(HL)
	INC	HL
	OR	A
	RET	Z
	LD	C,A
	CALL	OVEC
	JP	PMSG
;
;
OVEC::
	PUSH	HL
	PUSH	DE
	PUSH	BC
	LD	E,C
	LD	C,6
	CALL	BDOS
	POP	BC
	POP	DE
	POP	HL
	RET
;
;
BLANK	DEFL	0E5h

SECTBF::
	REPT	256
	DEFB	BLANK
	ENDM
  IF S512
	REPT	256
	DEFB	BLANK
	ENDM
  ENDIF

RDBUFF::
	REPT	256
	DEFB	0
	ENDM
  IF S512
	REPT	256
	DEFB	0
	ENDM
  ENDIF

	END	FORMAT
